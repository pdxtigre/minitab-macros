macro
#
#    Macro:  STANDARD2.MAC
#   Author:  Cathy Akritas; Anthony N.
#  Version:  Minitab 19
#			 9/16/2024 Ver2 with control limits from a subset specified by C3 C4 columns
#
#  This macro creates a standardized control chart for the fraction of 
#  non-conforming units.  
#
#  Suppose C1 contains the number of defects (or defectives) and C2 is the total
#  number of items that were tested.  Go to View > Show Command Line/History
#  In the Command Line pane, type the command
#
#    %Standard2 C1 C2 C3 C4
#
#  Click Run.  
#
#  Using an optional subcommand, you can use a column of dates to label the X axis.  
#  Suppose the dates are in C3.  In the Command Line pane, type
#
#    %Standard C1 C2 C3 C4;
#    Labels C5.
#
#  Click Run.
#
#  Reference:  D.C. Montgomery (2001). Introduction to Statistical Quality 
#              Control, 4th edition, John Wiley & Sons.
#
###########################################################################
#
#  Neither Minitab, LLC nor the author(s) of this MACRO makes any claim 
#  of or offers any Warranty whatsoever with regard to the accuracy of 
#  this MACRO or its suitability for use.  Minitab, LLC and the author(s)
#  of this MACRO each hereby disclaims any Warranty and/or liability with 
#  respect thereto. 
#
###########################################################################

standard2 Di ni D2i n2i;
  label date.

mcolumn  Di ni D2i n2i pi Zi date sigmai out
mconstant pbar ymax ymin cnt k1 cnt1 cnt2 title xname

brief 0
noecho
endmtitle

let cnt1 = count(Di)
let cnt2 = count(ni)

if cnt1 <> cnt2
 mtitle "Standardized Control Chart for Fraction of Non-Conforming";
    notitle.
  brief 1 
  print "   ** Error **  Input columns must have the same number of rows"
  print "                     Macro Exiting"  
  endmtitle
  exit
endif
if label = 1
  count date cnt
  if cnt <> cnt1
   mtitle "Standardized Control Chart for Fraction of Non-Conforming";
      notitle.
    brief 1 
    print "   ** Error **  All input columns must have the same number of rows"
    print "                     Macro Exiting"  
    endmtitle
    exit
  endif
endif

If ni = 0 
 mtitle "Standardized Control Chart for Fraction of Non-Conforming";
    notitle.
  brief 1 
  print "   ** Error **  Sample size must be greater than 0"
  print "                      Macro Exiting"
  endmtitle
  exit
endif

let pi = Di/ni

if label = 0
  count pi cnt
  set date 
    1:cnt
  end
  let k1 = "Sample Number"  
else 
  let k1 = "Date"
endif

let pbar = sum(D2i)/sum(n2i)

let sigmai = sqrt(pbar*(1-pbar)/ni)
let Zi = (pi-pbar)/sigmai
let out = (Zi > 3 or Zi< -3)
Code (0) 1 (1) 2 ('*') 1 out out
let ymax = maxi(zi)
rmax ymax 3 ymax
let ymax = ymax*1.1
let ymin = mini(zi)
rmin ymin -3 ymin
let ymin = ymin + ymin*.1

kkname xname Di
let title = concat("Standardized Control for Fraction of Defects for ", xname)

 mtitle "Standardized Control Chart for Fraction of Non-Conforming";
    notitle.
Tsplot Zi;
  wtitle "Standardized Control Chart for Fraction Non-conforming";
  title title;
  Index;
  stamp date;
  axlabel 1 k1;
  scale 2; 
    maxi ymax;
    mini ymin;
  Connect;
    Type 1;
    Color 1;
    Size 1;
  Symbol;
    Type 6;
    Color out;
    Size 1;
  Reference 2 3;
    Type 1;
    Color 2;
    Size 1;
  Reference 2 -3;
    Type 1;
    Color 2;
    Size 1;
  Reference 2 0;
    Type 1;
    Color 3;
    Size 1;
  Nodtitle.
endmtitle
endmacro